#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~//
# Copyright (c) 2016-22, Lawrence Livermore National Security, LLC
# and RAJA project contributors. See the RAJA/LICENSE file for details.
#
# SPDX-License-Identifier: (BSD-3-Clause)
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~//
#
#=== Usage ===================================================================
# This file allows RAJA to be automatically detected by other libraries
# using CMake.  To build with RAJA, you can do one of two things:
#
#   1. Set the RAJA_DIR environment variable to the root directory of the RAJA
#      installation.  If you loaded RAJA through a dotkit, this may already
#      be set, and RAJA will be autodetected by CMake.
#
#   2. Configure your project with this option:
#      -DRAJA_DIR=<RAJA install prefix>/share/
#
# If you have done either of these things, then CMake should automatically find
# and include this file when you call find_package(RAJA) from your
# CMakeLists.txt file.
#
#=== Components ==============================================================
#
# To link against these, just do, for example:
#
#   find_package(RAJA REQUIRED)
#   add_executable(foo foo.c)
#   target_link_libraries(foo RAJA)
#
# That's all!
#
@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if (@RAJA_NEEDS_BLT_TPLS@)
  if (@RAJA_ENABLE_CUDA@)
    find_dependency(Threads)
  endif ()
endif()

#Needs to come before camp 
#Camp performs a check on the BLT target
set(BLT_TGTS "${CMAKE_CURRENT_LIST_DIR}/bltTargets.cmake")
if(EXISTS "${BLT_TGTS}")
include("${BLT_TGTS}")
endif()
unset(BLT_TGTS)

if (NOT TARGET camp)
  set(RAJA_CAMP_DIR "@camp_DIR@")
  if(NOT camp_DIR) 
    set(camp_DIR ${RAJA_CAMP_DIR}) 
  endif()

  find_dependency(camp CONFIG NO_DEFAULT_PATH PATHS 
    ${camp_DIR}
    ${camp_DIR}/lib/cmake/camp
    @PACKAGE_CMAKE_INSTALL_PREFIX@
    @PACKAGE_CMAKE_INSTALL_PREFIX@/lib/cmake/camp)
endif ()

if (@RAJA_ENABLE_DESUL_ATOMICS@)
  if (NOT TARGET desul_atomics)
    set(RAJA_DESUL_DIR "@desul_DIR@")
    if(NOT desul_DIR) 
      set(desul_DIR ${RAJA_DESUL_DIR}) 
    endif()

    find_dependency(desul CONFIG NO_DEFAULT_PATH PATHS 
      ${desul_DIR}
      ${desul_DIR}/lib/cmake/desul
      @PACKAGE_CMAKE_INSTALL_PREFIX@
      @PACKAGE_CMAKE_INSTALL_PREFIX@/lib/cmake/desul)
  endif ()
endif()

set(BLT_TGTS "${CMAKE_CURRENT_LIST_DIR}/bltTargets.cmake")
if(EXISTS "${BLT_TGTS}")
include("${BLT_TGTS}")
endif()
unset(BLT_TGTS)

include("${CMAKE_CURRENT_LIST_DIR}/RAJATargets.cmake")
check_required_components("@PROJECT_NAME@")
